.macro end
    li      $v0, 10
    syscall
.end_macro

.macro getInt(%des)
    li      $v0, 5
    syscall
    move    %des, $v0
.end_macro

.macro getChar(%des)
	li      $v0, 12
	syscall
	move    %des, $v0
.end_macro

.macro printInt(%src)
    move    $a0, %src
    li      $v0, 1
    syscall
.end_macro

.macro printChar(%src)
	la $a0 %src
	li $v0 4
	syscall 
.end_macro

.macro push(%src)
    addi    $sp, $sp, -4
    sw      %src, 0($sp)
.end_macro

.macro pop(%des)
    lw      %des, 0($sp)
    addi    $sp, $sp, 4
.end_macro

.macro Index(%n, %m, %s0, %ans)
	mult %n %s0
	mflo %ans
	add %ans %ans %m
	sll %ans %ans 2
.end_macro

.data
	map: .space 200
	visited: .space 200

.text
main:
	getInt($s0)  #s0 = n
	getInt($s3)  #s3 = m
	li $s7 0     #s7 = ans = 0
	
	move $a0 $s0
	move $a1 $s3
	move $a2 $s3
	jal function_read
	
	getInt($s1)  #s1 = s1
	getInt($s2)  #s2 = s2
	getInt($s4)  #s4 = d1 
	getInt($s5)  #s5 = d2
	
	subi $s1 $s1 1
	subi $s2 $s2 1
	subi $s4 $s4 1
	subi $s5 $s5 1
	
	move $a0 $s1
	move $a1 $s2
	jal dfs
	
	printInt($s7)
	
end


function_read:
	push($ra)
	push($t0)
	push($t1)
	push($t2)
	push($t3)
	push($t4)
	push($t5)
	push($t6)
	
	move $t0 $a0 #t0 = n
	move $t1 $a1 #t1 = m
	move $t5 $a2 #t5 = s0
	
	li $t2 0     #t2 = i
for_read_i_start:
	slt $t4 $t2 $t0
	beq $t4 $0 for_read_i_end
	
	li $t3 0     #t3 = j
	for_read_j_start:
		slt $t4 $t3 $t1
		beq $t4 $0 for_read_j_end
		
		Index($t2, $t3, $t5, $t4)
		getInt($t6)
		sw $t6 map($t4)
		
		addi $t3 $t3 1
		j for_read_j_start
	for_read_j_end:
	
	
	addi $t2 $t2 1
	j for_read_i_start
for_read_i_end:
	pop($t6)
	pop($t5)
	pop($t4)
	pop($t3)
	pop($t2)
	pop($t1)
	pop($t0)
	pop($ra)
	
	jr $ra


dfs:
	push($ra)
	push($s1)
	push($s2)
	push($t0)
	push($t1)
	push($t2)
	push($t3)
	
	move $s1 $a0 
	move $s2 $a1 
	
	bne $s1 $s4 else
	bne $s2 $s5 else
	
	if:
		addi $s7 $s7 1
			
		j if_end
	else:
		Index($s1, $s2, $s3, $t0)
		li $t1 1
		sw $t1 visited($t0)  #visited[s1][s2] = 1
		
		subi $t0 $s1 1
		slt $t1 $t0 $0
		beq $t1 1 if_end1
		
		subi $t0 $s1 1
		Index($t0, $s2, $s3, $t1)
		lw $t1 map($t1)
		bne $t1 $0 if_end1
		
		Index($t0, $s2, $s3, $t1)
		lw $t1 visited($t1)
		bne $t1 $0 if_end1
		
		if1:
			move $a0 $t0
			move $a1 $s2
			jal dfs
		
		if_end1:
		
		addi $t0 $s1 1
		slt $t1 $t0 $s0
		beq $t1 0 if_end2
		
		addi $t0 $s1 1
		Index($t0, $s2, $s3, $t1)
		lw $t1 map($t1)
		bne $t1 $0 if_end2
		
		Index($t0, $s2, $s3, $t1)
		lw $t1 visited($t1)
		bne $t1 $0 if_end2
		
		if2:	
			move $a0 $t0
			move $a1 $s2
			jal dfs
		
		if_end2:
		
		subi $t0 $s2 1
		slt $t1 $t0 $0
		beq $t1 1 if_end3
		
		Index($s1, $t0, $s3, $t1)
		lw $t1 map($t1)
		bne $t1 $0 if_end3
		
		Index($s1, $t0, $s3, $t1)
		lw $t1 visited($t1)
		bne $t1 $0 if_end3
	
		if3:
			move $a0 $s1
			move $a1 $t0
			jal dfs
		
		if_end3:
		
		addi $t0 $s2 1
		slt $t1 $t0 $s3
		beq $t1 0 if_end4
		
		Index($s1, $t0, $s3, $t1)
		lw $t1 map($t1)
		bne $t1 $0 if_end4
		
		Index($s1, $t0, $s3, $t1)
		lw $t1 visited($t1)
		bne $t1 $0 if_end4
		
		if4:
			move $a0 $s1
			move $a1 $t0
			jal dfs
			
		if_end4:

		Index($s1, $s2, $s3, $t0)
		sw $0 visited($t0)
		
		if_end:
		
	pop($t3)
	pop($t2)
	pop($t1)	
	pop($t0)
	pop($s2)
	pop($s1)
	pop($ra)
	
	jr $ra
	
	
	

	

